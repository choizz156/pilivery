plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

configurations {
    asciidoctorExt
}

dependencies {
    //lombok
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    //db
    runtimeOnly 'com.h2database:h2'

    //restdocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    //rest-assured-mockmvc
    testImplementation 'io.rest-assured:spring-mock-mvc:4.5.1'
    testImplementation 'io.rest-assured:rest-assured-all'

    //jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //expiringMap
    implementation 'net.jodah:expiringmap:0.5.9'

    //jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    //security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    //fixturemonkey
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.14'
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-javax-validation:1.0.14'
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-jackson:1.0.14'

    implementation 'com.google.guava:guava:33.2.1-jre'

    //redis
    implementation 'org.redisson:redisson-spring-boot-starter:3.22.0'

}

ext { // 전역 변수
    snippetsDir = file('build/generated-snippets')
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다.
        include("**/index.adoc")
    }

    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.

}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}
